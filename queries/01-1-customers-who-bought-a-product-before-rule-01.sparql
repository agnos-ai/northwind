PREFIX        : <http://www.mysparql.com/resource/northwind/>
PREFIX kggraph: <http://www.mysparql.com/resource/northwind/graph/>

# Customers who bought product-61

SELECT DISTINCT # eliminates duplicates in case the same customer bought a product more than once
  ?customer 
  ?companyName 
  ?contactName
WHERE { 
  GRAPH kggraph:dataGraph {
    ?customer    a :Customer ;
                 :companyName ?companyName ;
                 :contactName ?contactName .
    ?order       a :Order ;
                 :hasCustomer ?customer .
    ?orderDetail a :OrderDetail ;
                 :hasProduct :product-61 ;
                 :belongsToOrder ?order .
  }
}
ORDER BY ?customer



# The following is the property path version of the query above


# A property path is a possible route through a graph between two graph nodes. A trivial case is a property path of length exactly 1, which is a triple pattern. The ends of the path may be RDF terms or variables. Variables can not be used as part of the path itself, only the ends.
# Property paths allow for more concise expressions for some SPARQL basic graph patterns and they also add the ability to match connectivity of two resources by an arbitrary length path.

PREFIX        : <http://www.mysparql.com/resource/northwind/>
PREFIX kggraph: <http://www.mysparql.com/resource/northwind/graph/>

SELECT DISTINCT
    ?customer
WHERE {
    GRAPH ?graph {
        ?customer ^:hasCustomer/^:belongsToOrder/:hasProduct :product-61 .
    }
}
ORDER BY ?customer



