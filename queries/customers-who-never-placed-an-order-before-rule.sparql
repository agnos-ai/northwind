PREFIX : <http://www.mysparql.com/resource/northwind/>

# Query: Customers who never placed an order
SELECT DISTINCT 
  ?customer
  ?companyName
  ?postalCode
  ?city
  ?country
WHERE { 
    GRAPH ?graph {
        ?customer a :Customer .
        ?customer   :customerID ?customerID ;
                    :companyName ?companyName ;
                    :city ?city ;
                    :country ?country .
        OPTIONAL {?customer :postalCode ?postalCode} . # Some regions don't use PostalCode.
        OPTIONAL {
            ?order a  :Order .
            ?customer ^:hasCustomer ?order # left join - for customers with no orders, ?order variable will be empty.
        } 
        FILTER (!BOUND(?order)) # Checks if variable is not bound to a value. 
    }
}
ORDER BY ?customer


# Query: Customers who never placed an order (using MINUS)
SELECT 
  ?customer
  ?companyName
  ?postalCode
  ?city
  ?country
WHERE { 
  {
    ?customer a :Customer ; # All customers
    :customerID ?customerID ;
    :companyName ?companyName ;
    :city ?city ;
    :country ?country .
  }
    MINUS
  {
    ?customer a :Customer . # Customers who placed orders
    ?order a  :Order .
    ?order :hasCustomer ?customer .
  }
}
  
